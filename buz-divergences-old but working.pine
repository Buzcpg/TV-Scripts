// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Buz rework of // Â© LonesomeTheBlue's many divergences script
// Built for CoffeePlz7759 discord
//v7 working nicely - added vol in - so far buy vol only seems to work ok but will be missing sell vol
//v8 - added in macd length
//v9 - added in Chaikin Osc
//15 - added pivot options
//18 - change minval of maxpp - (useless so reverted)
//v20 adjusted macd back to default 12 26 - prd set to 5 each way (was messing with consistency of indicators)
//v21 reset numbering
//v22 - prd test in the len > prd - might shorten trade distances
//v23 - default prd 2 and 2 by request
//v24 - added must haves in - untested
//v25 - must have tweaks

//@version=5
indicator('CoffeePlz7759 Divergences', overlay=true, max_bars_back=1000, max_lines_count=400, max_labels_count=400)
prd = input.int(defval=2, title='Pivot Period Left', minval=1, maxval=100)
prd1 = input.int(defval=2, title='Pivot Period Right', minval=1, maxval=100)
source = input.string(defval='Close', title='Source for Pivot Points', options=['Close', 'High/Low'])
searchdiv = input.string(defval='Regular', title='Divergence Type', options=['Regular', 'Hidden', 'Regular/Hidden'])
showindis = input.string(defval='Full', title='Show Indicator Names', options=['Full', 'First Letter', 'Don\'t Show'])
showlimit = input.int(1, title='Minimum Number of Divergence')
maxpp = input.int(defval=5, title='Maximum Pivot Points to Check', minval=1, maxval=50)
maxbars = input.int(defval=30, title='Maximum Bars to Check', minval=5, maxval=200)
shownum = input(defval=true, title='Show Divergence Number')
showlast = input(defval=false, title='Show Only Last Divergence')
dontconfirm = input(defval=true, title='Don\'t Wait for Confirmation')
showlines = input(defval=true, title='Show Divergence Lines')
showpivot = input(defval=false, title='Show Pivot Points')
calcmacd = input(defval=true, title='MACD', group = "----  Coffee's Choices  ----")
macdF = input(12, title="MACD Fast Length", group = "----  Coffee's Choices  ----", inline = "MACD")
macdS = input(26, title="MACD Slow Length", group = "----  Coffee's Choices  ----", inline = "MACD")
calcmacda = input(defval=false, title='MACD Histogram', group = "----  Coffee's Choices  ----")
buySellVol = input(defval = true, title='Buy/Sell Volume', group = "----  Coffee's Choices  ----")
calcobv = input(defval=true, title='OBV', group = "----  Coffee's Choices  ----")
chaik = input(defval=true, title='Chaikin Oscillator', group = "----  Coffee's Choices  ----")
chaikF = input(3, title="Chaikin Fast Length", group = "----  Coffee's Choices  ----", inline = "Chaikin")
chaikS = input(10, title="Chaikin Slow Length", group = "----  Coffee's Choices  ----", inline = "Chaikin")
calcrsi = input(defval=false, title='RSI', group = "----  Additional Indicators  ----")
calcstoc = input(defval=false, title='Stochastic', group = "----  Additional Indicators  ----")
calccci = input(defval=false, title='CCI', group = "----  Additional Indicators  ----")
calcmom = input(defval=false, title='Momentum', group = "----  Additional Indicators  ----")
calcvwmacd = input(false, title='VWmacd', group = "----  Additional Indicators  ----")
calccmf = input(false, title='Chaikin Money Flow', group = "----  Additional Indicators  ----")
calcmfi = input(false, title='Money Flow Index', group = "----  Additional Indicators  ----")
calcext = input(false, title='Check External Indicator', group = "----  Additional Indicators  ----")
externalindi = input(defval=close, title='External Indicator', group = "----  Additional Indicators  ----")
// Input for mandatory divergences
must_have_div1 = input.string(defval='None', title='Must-Have Divergence 1', options=['None', 'MACD', 'RSI', 'Buy/Sell Volume', 'OBV', 'Chai'])
must_have_div2 = input.string(defval='None', title='Must-Have Divergence 2', options=['None', 'MACD', 'RSI', 'Buy/Sell Volume', 'OBV', 'Chai'])
//
pos_reg_div_col = input(defval=color.rgb(22, 70, 47), title='Positive Regular Divergence', group = "----  Lines  ----")
neg_reg_div_col = input(defval=color.rgb(146, 21, 21), title='Negative Regular Divergence', group = "----  Lines  ----")
pos_hid_div_col = input(defval=color.lime, title='Positive Hidden Divergence', group = "----  Lines  ----")
neg_hid_div_col = input(defval=color.red, title='Negative Hidden Divergence', group = "----  Lines  ----")
pos_div_text_col = input(defval=color.white, title='Positive Divergence Text Color', group = "----  Lines  ----")
neg_div_text_col = input(defval=color.white, title='Negative Divergence Text Color', group = "----  Lines  ----")
reg_div_l_style_ = input.string(defval='Solid', title='Regular Divergence Line Style', options=['Solid', 'Dashed', 'Dotted'], group = "----  Lines  ----")
hid_div_l_style_ = input.string(defval='Dashed', title='Hdden Divergence Line Style', options=['Solid', 'Dashed', 'Dotted'], group = "----  Lines  ----")
reg_div_l_width = input.int(defval=2, title='Regular Divergence Line Width', minval=1, maxval=5, group = "----  Lines  ----")
hid_div_l_width = input.int(defval=1, title='Hidden Divergence Line Width', minval=1, maxval=5, group = "----  Lines  ----")
//showmas = input.bool(defval=false, title='Show MAs 50 & 200', inline='ma12')
//cma1col = input.color(defval=color.lime, title='', inline='ma12')
//cma2col = input.color(defval=color.red, title='', inline='ma12')

//plot(showmas ? ta.sma(close, 50) : na, color=showmas ? cma1col : na)
//plot(showmas ? ta.sma(close, 200) : na, color=showmas ? cma2col : na)

// set line styles
var reg_div_l_style = reg_div_l_style_ == 'Solid' ? line.style_solid : reg_div_l_style_ == 'Dashed' ? line.style_dashed : line.style_dotted
var hid_div_l_style = hid_div_l_style_ == 'Solid' ? line.style_solid : hid_div_l_style_ == 'Dashed' ? line.style_dashed : line.style_dotted

// get indicators
rsi = ta.rsi(close, 14)  // RSI
[macd, signal, deltamacd] = ta.macd(close, macdF, macdS, 9)  // MACD
moment = ta.mom(close, 10)  // Momentum
cci = ta.cci(close, 10)  // CCI
Obv = ta.obv  // OBV
stk = ta.sma(ta.stoch(close, high, low, 14), 3)  // Stoch
maFast = ta.vwma(close, 12)  // volume weighted macd
maSlow = ta.vwma(close, 26)
vwmacd = maFast - maSlow
Cmfm = (close - low - (high - close)) / (high - low)  // Chaikin money flow
Cmfv = Cmfm * volume
cmf = ta.sma(Cmfv, 21) / ta.sma(volume, 21)
Mfi = ta.mfi(close, 14)  // Moneyt Flow Index
cosc = ta.ema(ta.accdist, chaikF) - ta.ema(ta.accdist, chaikS)

// Buy and Sell Volume Calculation
buyVolume = high == low ? 0 : volume * (close - low) / (high - low)
sellVolume = high == low ? 0 : volume * (high - close) / (high - low)

// keep indicators names and colors in arrays
var indicators_name = array.new_string(13)
var div_colors = array.new_color(4)
if barstate.isfirst
    // names
    array.set(indicators_name, 0, showindis == 'Full' ? 'MACD' : 'M')
    array.set(indicators_name, 1, showindis == 'Full' ? 'Hist' : 'H')
    array.set(indicators_name, 2, showindis == 'Full' ? 'RSI' : 'E')
    array.set(indicators_name, 3, showindis == 'Full' ? 'Stoch' : 'S')
    array.set(indicators_name, 4, showindis == 'Full' ? 'CCI' : 'C')
    array.set(indicators_name, 5, showindis == 'Full' ? 'MOM' : 'M')
    array.set(indicators_name, 6, showindis == 'Full' ? 'OBV' : 'O')
    array.set(indicators_name, 7, showindis == 'Full' ? 'VWMACD' : 'V')
    array.set(indicators_name, 8, showindis == 'Full' ? 'CMF' : 'C')
    array.set(indicators_name, 9, showindis == 'Full' ? 'MFI' : 'M')
    array.set(indicators_name, 10, showindis == 'Full' ? 'Extrn' : 'X')
    array.set(indicators_name, 11, showindis == 'Full' ? 'VOL' : 'BS')
    array.set(indicators_name, 12, showindis == 'Full' ? 'CHAI' : 'CH')

    //colors
    array.set(div_colors, 0, pos_reg_div_col)
    array.set(div_colors, 1, neg_reg_div_col)
    array.set(div_colors, 2, pos_hid_div_col)
    array.set(div_colors, 3, neg_hid_div_col)

// Check if we get new Pivot High Or Pivot Low
float ph = ta.pivothigh(source == 'Close' ? close : high, prd, prd1)
float pl = ta.pivotlow(source == 'Close' ? close : low, prd, prd1)
plotshape(ph and showpivot, text='H', style=shape.labeldown, color=color.new(color.white, 100), textcolor=color.new(color.red, 0), location=location.abovebar, offset=-prd1)
plotshape(pl and showpivot, text='L', style=shape.labelup, color=color.new(color.white, 100), textcolor=color.new(color.lime, 0), location=location.belowbar, offset=-prd1)

// keep values and positions of Pivot Highs/Lows in the arrays
var int maxarraysize = 20
var ph_positions = array.new_int(maxarraysize, 0)
var pl_positions = array.new_int(maxarraysize, 0)
var ph_vals = array.new_float(maxarraysize, 0.)
var pl_vals = array.new_float(maxarraysize, 0.)

// add PHs to the array
if ph
    array.unshift(ph_positions, bar_index)
    array.unshift(ph_vals, ph)
    if array.size(ph_positions) > maxarraysize
        array.pop(ph_positions)
        array.pop(ph_vals)

// add PLs to the array
if pl
    array.unshift(pl_positions, bar_index)
    array.unshift(pl_vals, pl)
    if array.size(pl_positions) > maxarraysize
        array.pop(pl_positions)
        array.pop(pl_vals)

// functions to check Regular Divergences and Hidden Divergences

// function to check positive regular or negative hidden divergence
// cond == 1 => positive_regular, cond == 2=> negative_hidden
positive_regular_positive_hidden_divergence(src, cond) =>
    divlen = 0
    prsc = source == 'Close' ? close : low
    // if indicators higher than last value and close price is higher than las close 
    if dontconfirm or src > src[1] or close > close[1]
        startpoint = dontconfirm ? 0 : 1  // don't check last candle
        // we search last 15 PPs
        for x = 0 to maxpp - 1 by 1
            len = bar_index - array.get(pl_positions, x) + prd
            // if we reach non valued array element or arrived 101. or previous bars then we don't search more
            if array.get(pl_positions, x) == 0 or len > maxbars
                break
            if len > prd1 and (cond == 1 and src[startpoint] > src[len] and prsc[startpoint] < nz(array.get(pl_vals, x)) or cond == 2 and src[startpoint] < src[len] and prsc[startpoint] > nz(array.get(pl_vals, x)))
                slope1 = (src[startpoint] - src[len]) / (len - startpoint)
                virtual_line1 = src[startpoint] - slope1
                slope2 = (close[startpoint] - close[len]) / (len - startpoint)
                virtual_line2 = close[startpoint] - slope2
                arrived = true
                for y = 1 + startpoint to len - 1 by 1
                    if src[y] < virtual_line1 or nz(close[y]) < virtual_line2
                        arrived := false
                        break
                    virtual_line1 -= slope1
                    virtual_line2 -= slope2
                    virtual_line2

                if arrived
                    divlen := len
                    break
    divlen

// function to check negative regular or positive hidden divergence
// cond == 1 => negative_regular, cond == 2=> positive_hidden
negative_regular_negative_hidden_divergence(src, cond) =>
    divlen = 0
    prsc = source == 'Close' ? close : high
    // if indicators higher than last value and close price is higher than las close 
    if dontconfirm or src < src[1] or close < close[1]
        startpoint = dontconfirm ? 0 : 1  // don't check last candle
        // we search last 15 PPs
        for x = 0 to maxpp - 1 by 1
            len = bar_index - array.get(ph_positions, x) + prd
            // if we reach non valued array element or arrived 101. or previous bars then we don't search more
            if array.get(ph_positions, x) == 0 or len > maxbars
                break
            if len > prd1 and (cond == 1 and src[startpoint] < src[len] and prsc[startpoint] > nz(array.get(ph_vals, x)) or cond == 2 and src[startpoint] > src[len] and prsc[startpoint] < nz(array.get(ph_vals, x)))
                slope1 = (src[startpoint] - src[len]) / (len - startpoint)
                virtual_line1 = src[startpoint] - slope1
                slope2 = (close[startpoint] - nz(close[len])) / (len - startpoint)
                virtual_line2 = close[startpoint] - slope2
                arrived = true
                for y = 1 + startpoint to len - 1 by 1
                    if src[y] > virtual_line1 or nz(close[y]) > virtual_line2
                        arrived := false
                        break
                    virtual_line1 -= slope1
                    virtual_line2 -= slope2
                    virtual_line2

                if arrived
                    divlen := len
                    break
    divlen

// calculate 4 types of divergence if enabled in the options and return divergences in an array
calculate_divs(cond, indicator_1) =>
    divs = array.new_int(4, 0)
    array.set(divs, 0, cond and (searchdiv == 'Regular' or searchdiv == 'Regular/Hidden') ? positive_regular_positive_hidden_divergence(indicator_1, 1) : 0)
    array.set(divs, 1, cond and (searchdiv == 'Regular' or searchdiv == 'Regular/Hidden') ? negative_regular_negative_hidden_divergence(indicator_1, 1) : 0)
    array.set(divs, 2, cond and (searchdiv == 'Hidden' or searchdiv == 'Regular/Hidden') ? positive_regular_positive_hidden_divergence(indicator_1, 2) : 0)
    array.set(divs, 3, cond and (searchdiv == 'Hidden' or searchdiv == 'Regular/Hidden') ? negative_regular_negative_hidden_divergence(indicator_1, 2) : 0)
    divs

// Must have checks

// Check for mandatory divergences before plotting
var bool mandatory_div1_present = true
var bool mandatory_div2_present = true

// Check if the first mandatory divergence is present
if must_have_div1 != 'None'
    mandatory_div1_present := false  // Initially assume the divergence is not present
    if must_have_div1 == 'MACD'
        macd_divs = calculate_divs(calcmacd, macd)
        mandatory_div1_present := array.get(macd_divs, 0) > 0 or array.get(macd_divs, 1) > 0 or array.get(macd_divs, 2) > 0 or array.get(macd_divs, 3) > 0

    if must_have_div1 == 'RSI'
        rsi_divs = calculate_divs(calcrsi, rsi)
        mandatory_div1_present := array.get(rsi_divs, 0) > 0 or array.get(rsi_divs, 1) > 0 or array.get(rsi_divs, 2) > 0 or array.get(rsi_divs, 3) > 0

    if must_have_div1 == 'Stochastic'
        stoch_divs = calculate_divs(calcstoc, stk)
        mandatory_div1_present := array.get(stoch_divs, 0) > 0 or array.get(stoch_divs, 1) > 0 or array.get(stoch_divs, 2) > 0 or array.get(stoch_divs, 3) > 0

    if must_have_div1 == 'Obv'
        Obv_divs = calculate_divs(calcobv, Obv)
        mandatory_div1_present := array.get(Obv_divs, 0) > 0 or array.get(Obv_divs, 1) > 0 or array.get(Obv_divs, 2) > 0 or array.get(Obv_divs, 3) > 0

    if must_have_div1 == 'Chai'
        chai_divs = calculate_divs(chaik, cosc)
        mandatory_div1_present := array.get(chai_divs, 0) > 0 or array.get(chai_divs, 1) > 0 or array.get(chai_divs, 2) > 0 or array.get(chai_divs, 3) > 0

// Check if the second mandatory divergence is present
if must_have_div2 != 'None'
    mandatory_div2_present := false  // Initially assume the divergence is not present
    if must_have_div2 == 'MACD'
        macd_divs = calculate_divs(calcmacd, macd)
        mandatory_div2_present := array.get(macd_divs, 0) > 0 or array.get(macd_divs, 1) > 0 or array.get(macd_divs, 2) > 0 or array.get(macd_divs, 3) > 0

    if must_have_div2 == 'RSI'
        rsi_divs = calculate_divs(calcrsi, rsi)
        mandatory_div2_present := array.get(rsi_divs, 0) > 0 or array.get(rsi_divs, 1) > 0 or array.get(rsi_divs, 2) > 0 or array.get(rsi_divs, 3) > 0

    if must_have_div2 == 'Stochastic'
        stoch_divs = calculate_divs(calcstoc, stk)
        mandatory_div2_present := array.get(stoch_divs, 0) > 0 or array.get(stoch_divs, 1) > 0 or array.get(stoch_divs, 2) > 0 or array.get(stoch_divs, 3) > 0

    if must_have_div2 == 'Obv'
        Obv_divs = calculate_divs(calcobv, Obv)
        mandatory_div2_present := array.get(Obv_divs, 0) > 0 or array.get(Obv_divs, 1) > 0 or array.get(Obv_divs, 2) > 0 or array.get(Obv_divs, 3) > 0

    if must_have_div2 == 'Chai'
        chai_divs = calculate_divs(chaik, cosc)
        mandatory_div2_present := array.get(chai_divs, 0) > 0 or array.get(chai_divs, 1) > 0 or array.get(chai_divs, 2) > 0 or array.get(chai_divs, 3) > 0

// array to keep all divergences
var all_divergences = array.new_int(52)  // 13 indicators * 4 divergence = 52 elements
// set related array elements
array_set_divs(div_pointer, index) =>
    for x = 0 to 3 by 1
        array.set(all_divergences, index * 4 + x, array.get(div_pointer, x))

// If mandatory divergences are required and exist, proceed with plotting
if mandatory_div1_present and mandatory_div2_present
    // Plot other divergences only if the mandatory ones are present or not required
    array_set_divs(calculate_divs(calcmacd, macd), 0)
    array_set_divs(calculate_divs(calcmacda, deltamacd), 1)
    array_set_divs(calculate_divs(calcrsi, rsi), 2)
    array_set_divs(calculate_divs(calcstoc, stk), 3)
    array_set_divs(calculate_divs(calccci, cci), 4)
    array_set_divs(calculate_divs(calcmom, moment), 5)
    array_set_divs(calculate_divs(calcobv, Obv), 6)
    array_set_divs(calculate_divs(calcvwmacd, vwmacd), 7)
    array_set_divs(calculate_divs(calccmf, cmf), 8)
    array_set_divs(calculate_divs(calcmfi, Mfi), 9)
    array_set_divs(calculate_divs(calcext, externalindi), 10)
    array_set_divs(calculate_divs(buySellVol, buyVolume), 11)
    array_set_divs(calculate_divs(chaik, cosc), 12)

// check minimum number of divergence, if less than showlimit then delete all divergence
total_div = 0
for x = 0 to array.size(all_divergences) - 1 by 1
    total_div += math.round(math.sign(array.get(all_divergences, x)))
    total_div

if total_div < showlimit
    array.fill(all_divergences, 0)

// keep line in an array
var pos_div_lines = array.new_line(0)
var neg_div_lines = array.new_line(0)
var pos_div_labels = array.new_label(0)
var neg_div_labels = array.new_label(0)

// remove old lines and labels if showlast option is enabled
delete_old_pos_div_lines() =>
    if array.size(pos_div_lines) > 0
        for j = 0 to array.size(pos_div_lines) - 1 by 1
            line.delete(array.get(pos_div_lines, j))
        array.clear(pos_div_lines)

delete_old_neg_div_lines() =>
    if array.size(neg_div_lines) > 0
        for j = 0 to array.size(neg_div_lines) - 1 by 1
            line.delete(array.get(neg_div_lines, j))
        array.clear(neg_div_lines)

delete_old_pos_div_labels() =>
    if array.size(pos_div_labels) > 0
        for j = 0 to array.size(pos_div_labels) - 1 by 1
            label.delete(array.get(pos_div_labels, j))
        array.clear(pos_div_labels)

delete_old_neg_div_labels() =>
    if array.size(neg_div_labels) > 0
        for j = 0 to array.size(neg_div_labels) - 1 by 1
            label.delete(array.get(neg_div_labels, j))
        array.clear(neg_div_labels)

// delete last created lines and labels until we met new PH/PV 
delete_last_pos_div_lines_label(n) =>
    if n > 0 and array.size(pos_div_lines) >= n
        asz = array.size(pos_div_lines)
        for j = 1 to n by 1
            line.delete(array.get(pos_div_lines, asz - j))
            array.pop(pos_div_lines)
        if array.size(pos_div_labels) > 0
            label.delete(array.get(pos_div_labels, array.size(pos_div_labels) - 1))
            array.pop(pos_div_labels)

delete_last_neg_div_lines_label(n) =>
    if n > 0 and array.size(neg_div_lines) >= n
        asz = array.size(neg_div_lines)
        for j = 1 to n by 1
            line.delete(array.get(neg_div_lines, asz - j))
            array.pop(neg_div_lines)
        if array.size(neg_div_labels) > 0
            label.delete(array.get(neg_div_labels, array.size(neg_div_labels) - 1))
            array.pop(neg_div_labels)

// variables for Alerts
pos_reg_div_detected = false
neg_reg_div_detected = false
pos_hid_div_detected = false
neg_hid_div_detected = false

// to remove lines/labels until we met new // PH/PL
var last_pos_div_lines = 0
var last_neg_div_lines = 0
var remove_last_pos_divs = false
var remove_last_neg_divs = false
if pl
    remove_last_pos_divs := false
    last_pos_div_lines := 0
    last_pos_div_lines
if ph
    remove_last_neg_divs := false
    last_neg_div_lines := 0
    last_neg_div_lines

// draw divergences lines and labels
divergence_text_top = ''
divergence_text_bottom = ''
distances = array.new_int(0)
dnumdiv_top = 0
dnumdiv_bottom = 0
top_label_col = color.white
bottom_label_col = color.white
old_pos_divs_can_be_removed = true
old_neg_divs_can_be_removed = true
startpoint = dontconfirm ? 0 : 1  // used for don't confirm option

for x = 0 to 12 by 1
    div_type = -1
    for y = 0 to 3 by 1
        if array.get(all_divergences, x * 4 + y) > 0  // any divergence?
            div_type := y
            if y % 2 == 1
                dnumdiv_top += 1
                top_label_col := array.get(div_colors, y)
                top_label_col
            if y % 2 == 0
                dnumdiv_bottom += 1
                bottom_label_col := array.get(div_colors, y)
                bottom_label_col
            if not array.includes(distances, array.get(all_divergences, x * 4 + y))  // line not exist ?
                array.push(distances, array.get(all_divergences, x * 4 + y))
                new_line = showlines ? line.new(x1=bar_index - array.get(all_divergences, x * 4 + y), y1=source == 'Close' ? close[array.get(all_divergences, x * 4 + y)] : y % 2 == 0 ? low[array.get(all_divergences, x * 4 + y)] : high[array.get(all_divergences, x * 4 + y)], x2=bar_index - startpoint, y2=source == 'Close' ? close[startpoint] : y % 2 == 0 ? low[startpoint] : high[startpoint], color=array.get(div_colors, y), style=y < 2 ? reg_div_l_style : hid_div_l_style, width=y < 2 ? reg_div_l_width : hid_div_l_width) : na
                if y % 2 == 0
                    if old_pos_divs_can_be_removed
                        old_pos_divs_can_be_removed := false
                        if not showlast and remove_last_pos_divs
                            delete_last_pos_div_lines_label(last_pos_div_lines)
                            last_pos_div_lines := 0
                            last_pos_div_lines
                        if showlast
                            delete_old_pos_div_lines()
                    array.push(pos_div_lines, new_line)
                    last_pos_div_lines += 1
                    remove_last_pos_divs := true
                    remove_last_pos_divs

                if y % 2 == 1
                    if old_neg_divs_can_be_removed
                        old_neg_divs_can_be_removed := false
                        if not showlast and remove_last_neg_divs
                            delete_last_neg_div_lines_label(last_neg_div_lines)
                            last_neg_div_lines := 0
                            last_neg_div_lines
                        if showlast
                            delete_old_neg_div_lines()
                    array.push(neg_div_lines, new_line)
                    last_neg_div_lines += 1
                    remove_last_neg_divs := true
                    remove_last_neg_divs

            // set variables for alerts
            if y == 0
                pos_reg_div_detected := true
                pos_reg_div_detected
            if y == 1
                neg_reg_div_detected := true
                neg_reg_div_detected
            if y == 2
                pos_hid_div_detected := true
                pos_hid_div_detected
            if y == 3
                neg_hid_div_detected := true
                neg_hid_div_detected
    // get text for labels
    if div_type >= 0
        divergence_text_top += (div_type % 2 == 1 ? showindis != 'Don\'t Show' ? array.get(indicators_name, x) + '\n' : '' : '')
        divergence_text_bottom += (div_type % 2 == 0 ? showindis != 'Don\'t Show' ? array.get(indicators_name, x) + '\n' : '' : '')
        divergence_text_bottom


// draw labels
if showindis != 'Don\'t Show' or shownum
    if shownum and dnumdiv_top > 0
        divergence_text_top += str.tostring(dnumdiv_top)
        divergence_text_top
    if shownum and dnumdiv_bottom > 0
        divergence_text_bottom += str.tostring(dnumdiv_bottom)
        divergence_text_bottom
    if divergence_text_top != ''
        if showlast
            delete_old_neg_div_labels()
        array.push(neg_div_labels, label.new(x=bar_index, y=math.max(high, high[1]), text=divergence_text_top, color=top_label_col, textcolor=neg_div_text_col, style=label.style_label_down))

    if divergence_text_bottom != ''
        if showlast
            delete_old_pos_div_labels()
        array.push(pos_div_labels, label.new(x=bar_index, y=math.min(low, low[1]), text=divergence_text_bottom, color=bottom_label_col, textcolor=pos_div_text_col, style=label.style_label_up))


alertcondition(pos_reg_div_detected, title='Positive Regular Divergence Detected', message='Positive Regular Divergence Detected')
alertcondition(neg_reg_div_detected, title='Negative Regular Divergence Detected', message='Negative Regular Divergence Detected')
alertcondition(pos_hid_div_detected, title='Positive Hidden Divergence Detected', message='Positive Hidden Divergence Detected')
alertcondition(neg_hid_div_detected, title='Negative Hidden Divergence Detected', message='Negative Hidden Divergence Detected')

alertcondition(pos_reg_div_detected or pos_hid_div_detected, title='Positive Divergence Detected', message='Positive Divergence Detected')
alertcondition(neg_reg_div_detected or neg_hid_div_detected, title='Negative Divergence Detected', message='Negative Divergence Detected')

